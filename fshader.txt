#version 430

in vec3 GNormal;
in vec3 GPosition;
noperspective in vec3 GEdgeDistance;

in vec3 GLightDir;
in vec3 GViewDir;
in vec2 GTexCoord;

layout (binding=0) uniform sampler2D ColorTex;
layout (binding=1) uniform sampler2D NormalMapTex;

struct LightInfo {
    vec4 Position;  // Light position in eye coords
    vec3 Intensity; // Light intensity
};

uniform LightInfo Light;

struct MaterialInfo {
    vec3  Ka;        // Ambient  reflectivity
    vec3  Kd;        // Diffuse  reflectivity
    vec3  Ks;        // Specular reflectivity
    float Shininess; // Specular shininess factor
};

uniform MaterialInfo Material;

out vec4 FragColor;

vec3 phongModel( vec3 norm, vec3 diffR ) {
    vec3 r         = reflect (-GLightDir, norm);

    float sDotN    = max(dot(GLightDir, norm), 0.0);

    vec3  ambient  = Light.Intensity * Material.Ka;
    vec3  diffuse  = Light.Intensity * diffR * sDotN;

    vec3  spec       = vec3(0.0);
    if (sDotN > 0.0) {
        spec = Light.Intensity * Material.Ks * pow(max(dot(r,GViewDir), 0.0), Material.Shininess);
    }

    return ambient + diffuse + spec;
}

void main()
{
    float LineWidth = 0.75;
    vec4  LineColor = vec4 (1.0, 0.0, 0.0, 0.0);

    vec4 normal = 2.0 * texture(NormalMapTex, GTexCoord) - 1.0;
/*
    if( gl_FrontFacing ) {
        phongModel(Position, Normal, ambAndDiff, spec);
    } else {
        phongModel(Position, -Normal, ambAndDiff, spec);
    }
*/
    vec4 texColor = texture(ColorTex, GTexCoord);
    vec4 color    = vec4 (phongModel(normal.xyz, texColor.rgb), 1.0);

    //Find the smallest distance
    float d = min (GEdgeDistance.x, GEdgeDistance.y);
    d       = min (d, GEdgeDistance.z);

    //Determine the mix factor with the line color
    float mixVal = smoothstep(LineWidth - 1.0, LineWidth + 1.0, d);

    //Mix the surface color with the line color
    FragColor = mix (LineColor, color, mixVal);

}
