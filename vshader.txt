#version 430

layout (location = 0) in  vec3 VertexPosition;
layout (location = 1) in  vec3 VertexNormal;
layout (location = 2) in  vec2 VertexTexCoord;
layout (location = 3) in  vec4 VertexTexTangent;

struct LightInfo {
    vec4 Position;  // Light position in eye coords
    vec3 Intensity; // Light intensity
};

uniform LightInfo Light;

out vec3 LightDir;
out vec3 ViewDir;
out vec2 TexCoord;

uniform mat4 ModelViewMatrix;
uniform mat3 NormalMatrix;
uniform mat4 MVP;                // Projection * Modelview

void main()
{
    // Convert normal, position and tangent to eye coords.
    vec3 Normal    = normalize(NormalMatrix * VertexNormal);
    vec3 Position  = vec3(ModelViewMatrix * vec4(VertexPosition, 1.0));
    vec3 Tangent   = normalize(NormalMatrix * vec3(VertexTexTangent));

    // Compute binormal
    vec3 Binormal  = normalize(cross(Normal, Tangent)) * VertexTexTangent.w;

    // Matrix to transform to tangent space
    mat3 toObjectLocal = mat3(
        Tangent.x, Binormal.x, Normal.x,
        Tangent.y, Binormal.y, Normal.y,
        Tangent.z, Binormal.z, Normal.z
    );

    // Transform Lightdir & Viewdir to tangent space
    LightDir  = normalize(toObjectLocal * (Light.Position.xyz - Position));
    ViewDir   = toObjectLocal * normalize(-Position);

    TexCoord  = VertexTexCoord;

    gl_Position = MVP * vec4(VertexPosition, 1.0);
}
